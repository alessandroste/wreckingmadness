apply plugin: 'com.android.application'

def secretsFile = rootProject.file("secrets.properties")
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    defaultConfig {
        namespace "com.ales.wreckingmadness"
        applicationId "com.ales.wreckingmadness"
        compileSdk PROP_TARGET_SDK_VERSION.toInteger()
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 12
        versionName "5.1"

        externalNativeBuild {
            cmake {
                targets 'wreckingmadness'
                arguments "-DCMAKE_FIND_ROOT_PATH=",
                    "-DANDROID_TOOLCHAIN=clang",
                    "-DANDROID_ARM_NEON=TRUE",
                    "-DANDROID_STL=c++_static",
                    "-DANDROID_CPP_FEATURES=exceptions rtti",
                    "-DUSE_SDKBOX=TRUE",
                    "-DCMAKE_VERBOSE_MAKEFILE=1"
                cppFlags "-fsigned-char"

                ["SERVICE_API_KEY", "SERVICE_BASE_URL"].forEach {
                    if (secrets.getProperty(it) != null) {
                        arguments "-D${it}=${secrets[it]}"
                    }
                }
            }
        }
    }

    sourceSets {
        main {
            assets.srcDir "../../Resources"
        }
    }

    ndkVersion '27.0.12077973'
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            version "3.22.1"
        }
    }
    
    if (secrets.getProperty("RELEASE_STORE_FILE") != null) {
        signingConfigs.register("release") {
            storeFile file(secrets.getProperty("RELEASE_STORE_FILE"))
            storePassword secrets.getProperty("RELEASE_STORE_PASSWORD")
            keyAlias secrets.getProperty("RELEASE_KEY_ALIAS")
            keyPassword secrets.getProperty("RELEASE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.findByName("release")
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
            }
        }
        debug {
            debuggable true
            jniDebuggable true
            applicationIdSuffix '.dbg'
            versionNameSuffix '-dbg'
            ndk {
                abiFilters  "arm64-v8a", "x86_64"
            }
        }
    }

    applicationVariants.configureEach { variant ->
        def dest_assets_folder = "${projectDir}/assets"

        // delete previous files first
        delete dest_assets_folder
        def targetName = variant.name.capitalize()
        def copyTaskName = "copy${targetName}ResourcesToAssets"

        tasks.register(copyTaskName) {
            copy {
                from layout.projectDirectory.dir("../Resources")
                into layout.buildDirectory.dir("intermediate/assets/${variant.dirName}")
                exclude "**/*.gz"
            }
        }

        tasks.named("pre${targetName}Build").configure {
            dependsOn copyTaskName
        }
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':libcocos2dx')
}
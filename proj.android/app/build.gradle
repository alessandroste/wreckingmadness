apply plugin: 'com.android.application'

def secretsFile = rootProject.file("secrets.properties")
def secrets = new Properties()
if (secretsFile.exists()) {
    secrets.load(new FileInputStream(secretsFile))
}

android {
    defaultConfig {
        namespace "com.ales.wreckingmadness"
        applicationId "com.ales.wreckingmadness"
        compileSdk PROP_TARGET_SDK_VERSION.toInteger()
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 11
        versionName "5.0"

        externalNativeBuild {
            cmake {
                targets 'wreckingmadness'
                arguments "-DCMAKE_FIND_ROOT_PATH=",
                    "-DANDROID_TOOLCHAIN=clang",
                    "-DANDROID_ARM_NEON=TRUE",
                    "-DANDROID_STL=c++_static",
                    "-DUSE_SDKBOX=TRUE"
                cppFlags "-frtti -fexceptions -fsigned-char"
            }
        }

        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86_64"
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "../../Resources"
    }

    ndkVersion '27.0.12077973'

    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
            version "3.22.1"
        }
    }
    
    signingConfigs {
        release {
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }

            if (secrets.hasProperty("SERVICE_KEY")) {
                externalNativeBuild {
                    cmake {
                        cppFlags.addAll(["-DSERVICE_KEY="+secrets["SERVICE_KEY"]])
                    }
                }
            }

            if (secrets.hasProperty("SERVICE_BASE_URI")) {
                externalNativeBuild {
                    cmake {
                        cppFlags.addAll(["-DSERVICE_BASE_URI="+secrets["SERVICE_BASE_URI"]])
                    }
                }
            }
        }
        debug {
            ndk {
                abiFilters  'arm64-v8a', "x86_64"
            }
        }
    }
}

android.applicationVariants.configureEach { variant ->
    def dest_assets_folder = "${projectDir}/assets"

    // delete previous files first
    delete dest_assets_folder
    def targetName = variant.name.capitalize()
    def copyTaskName = "copy${targetName}ResourcesToAssets"
    
    tasks.register(copyTaskName) {
        copy {
            from layout.projectDirectory.dir("../Resources")
            into layout.buildDirectory.dir("intermediate/assets/${variant.dirName}")
            exclude "**/*.gz"
        }
    }
    
    tasks.named("pre${targetName}Build").configure {
        dependsOn copyTaskName
    }
}

dependencies {
    implementation 'com.google.android.gms:play-services-ads:23.2.0'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api project(':libcocos2dx')
}